repositories {
    maven {
        name("NeoForge")
        url("https://maven.neoforged.net")
    }
}

dependencies {
    compileOnly("net.neoforged.fancymodloader:core:${project.property("dependencies.fancymodloader_version")}")
    compileOnly("net.neoforged.fancymodloader:language-java:${project.property("dependencies.fancymodloader_version")}")
    compileOnly("net.neoforged.fancymodloader:loader:${project.property("dependencies.fancymodloader_version")}")
}

java {
    sourceCompatibility(JavaVersion.VERSION_17)
    targetCompatibility(JavaVersion.VERSION_17)
}

processResources {
    filesMatching(["META-INF/mods.toml", "META-INF/neoforge.mods.toml"]) {
        expand([
                "mod_description": project.parent.property("mod.description"),
                "mod_homepage"   : project.parent.property("mod.homepage"),
                "mod_id"         : project.parent.property("mod.id").toString().replace("-", "_"),
                "mod_license"    : project.parent.property("mod.license"),
                "mod_name"       : project.parent.property("mod.name"),
                "mod_version"    : project.getVersionWithCommitHash(this.project.parent)
        ])
    }

    from("${rootDir}/LICENSE")
    from("${rootDir}/icon.png")

    from("${rootDir}/icon.png") {
        into("assets/${project.parent.property("mod.id")}")
    }
}

tasks.withType(Jar).configureEach {
    manifest.attributes(
            "MixinConfigs": "magiclib-core-bootstrap.mixins.json"
    )
}
