repositories {
    maven {
        name("Forge")
        url("https://maven.minecraftforge.net")
    }
}

dependencies {
    compileOnly("net.minecraftforge:forge:${project.property("dependencies.forge_version")}:universal")
    compileOnly("net.minecraftforge:forge:${project.property("dependencies.forge_version")}:launcher")
    compileOnly("net.minecraftforge:forgespi:${project.property("dependencies.forgespi_version")}")
    compileOnly("cpw.mods:modlauncher:${project.property("dependencies.modlauncher_version")}")
    compileOnly("org.apache.maven:maven-artifact:${project.property("dependencies.maven-artifact_version")}")
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand([
                "mod_description": project.parent.property("mod.description"),
                "mod_homepage"   : project.parent.property("mod.homepage"),
                "mod_id"         : project.parent.property("mod.id").toString().replace("-", "_"),
                "mod_license"    : project.parent.property("mod.license"),
                "mod_name"       : project.parent.property("mod.name"),
                "mod_version"    : project.getVersionWithCommitHash(this.project.parent)
        ])
    }

    from("${rootDir}/LICENSE")
    from("${rootDir}/icon.png")

    from("${rootDir}/icon.png") {
        into("assets/${project.parent.property("mod.id")}")
    }
}

tasks.withType(Jar).configureEach {
    manifest.attributes(
            "MixinConfigs": "magiclib-core-bootstrap.mixins.json"
    )
}
